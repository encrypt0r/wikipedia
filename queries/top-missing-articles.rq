# TOP 1000 missing articles about cities by site link count

SELECT ?e ?desc ?linkcount WHERE {
  # The entity is a city
  ?e wdt:P31 wd:Q515;
  # Get number of links (wikipedia languages)
  wikibase:sitelinks ?linkcount.
  
  # Don't inclue the entities that ‚ùå
  MINUS {
    # Have articles on sorani wikipedia
    ?article schema:about ?e;
    schema:isPartOf <https://ckb.wikipedia.org/>.
  }
  
  # Get label for the entity
  ?e rdfs:label ?desc.
  
  # Only get the english label
  FILTER((LANG(?desc)) = "en")
  
  FILTER(?linkcount >= 25) # Should have at least 25 links
}
GROUP BY ?e ?desc ?linkcount
ORDER BY DESC (?linkcount) # Sort by number of links, from highest to lowest
LIMIT 1000 # Only get top 1000
